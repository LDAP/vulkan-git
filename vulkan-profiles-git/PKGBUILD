#Maintainer: archdevlab <https://github.com/archdevlab>

pkgname=vulkan-profiles-git
pkgdesc='Vulkan header files (git version)'
pkgver=1.3.268.0.r94.g0255c1a
pkgrel=1
arch=(any)
url='https://github.com/KhronosGroup/Vulkan-Profiles'
license=(Apache-2.0)
makedepends=(cmake git ninja make valijson jsoncpp python-jsonschema vulkan-headers vulkan-icd-loader vulkan-utility-libraries)
conflicts=(vulkan-profiles)
provides=(vulkan-profiles)
source=(git+https://github.com/KhronosGroup/Vulkan-Profiles.git)

pkgver(){
  cd "${srcdir}"/Vulkan-Profiles
  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^vulkan-sdk-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  rm -rf "${srcdir}"/build

  "${srcdir}"/Vulkan-Profiles/scripts/update_deps.py --config release

  cmake -B "${srcdir}"/build -S "${srcdir}"/Vulkan-Profiles \
  -G "Unix Makefiles" \
  -D CMAKE_BUILD_TYPE=Release \
  -D CMAKE_INSTALL_PREFIX=/usr \
  -D CMAKE_INSTALL_LIBDIR=lib \
  -D CMAKE_INSTALL_SYSCONFDIR=/etc \
  -D CMAKE_INSTALL_DATADIR=/share \
  -D VULKAN_HEADERS_INSTALL_DIR=/usr \
  -D VULKAN_UTILITY_LIBRARIES_INSTALL_DIR=/usr \
  -D VULKAN_LOADER_INSTALL_DIR=/usr \
  -D JSONCPP_INSTALL_DIR=/usr \
  -D VALIJSON_INSTALL_DIR=/usr \
  -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/Vulkan-Profiles/LICENSES/* "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
