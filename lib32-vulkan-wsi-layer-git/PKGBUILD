#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Marijn Suijten <marijns95 at gmail dot com>

pkgname=lib32-vulkan-wsi-layer-git
pkgdesc='VulkanÂ® Window System Integration Layer'
pkgver=1.3.216.r110.gbf7d090
_major=1.3
pkgrel=1
arch=(x86_64)
url='https://gitlab.freedesktop.org/mesa/vulkan-wsi-layer'
license=(MIT)
# For the layer JSON description
depends=(lib32-vulkan-icd-loader vulkan-wsi-layer)
makedepends=(git cmake ninja vulkan-headers)
conflicts=(lib32-vulkan-wsi-layer)
provides=(lib32-vulkan-wsi-layer)
source=(git+https://gitlab.freedesktop.org/mesa/vulkan-wsi-layer.git)

pkgver(){
  cd "${srcdir}"/vulkan-wsi-layer
  echo "${_major}".$(sed -n 7p layer/VkLayer_window_system_integration.json | sed 's/[^0-9]*//g' | cut -c 3-).r$(git rev-list --count HEAD).g$(git rev-parse --short --abbrev=7 HEAD)
}

build(){
export CFLAGS+=" -m32"
export CXXFLAGS+=" -m32"
export LDFLAGS+=" -m32"
export ASFLAGS+=" --32"
export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/vulkan-wsi-layer \
  -D UPDATE_DEPS=OFF \
  -G "Unix Makefiles" \
  -D CMAKE_C_FLAGS=-m32 \
  -D CMAKE_CXX_FLAGS=-m32 \
  -D CMAKE_BUILD_TYPE=Release \
  -D CMAKE_INSTALL_PREFIX=/usr \
  -D CMAKE_INSTALL_BINDIR=bin \
  -D CMAKE_INSTALL_LIBDIR=lib32 \
  -D CMAKE_INSTALL_INCLUDEDIR=include \
  -D CMAKE_INSTALL_SYSCONFDIR=/etc \
  -D CMAKE_INSTALL_DATADIR=share \
  -D VULKAN_CXX_INCLUDE=/usr \
  -D BUILD_WSI_HEADLESS=ON \
  -D BUILD_WSI_WAYLAND=OFF \
  -D BUILD_WSI_IMAGE_COMPRESSION_CONTROL_SWAPCHAIN=ON \
  -D SELECT_EXTERNAL_ALLOCATOR="none" \
  -D EXTERNAL_WSIALLOC_LIBRARY="" \
  -D BUILD_DRM_UTILS=ON \
  -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  install -Dm644 "${srcdir}"/build/*{.a,.so} -t "${pkgdir}/usr/lib32/"

  # install licences
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/vulkan-wsi-layer/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
