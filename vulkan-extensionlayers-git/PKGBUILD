# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>

pkgname=vulkan-extensionlayers-git
pkgdesc='Layer providing Vulkan features when native support is unavailable (git version)'
pkgver=1.3.241
pkgrel=1
arch=(x86_64)
url='https://github.com/KhronosGroup/Vulkan-ExtensionLayer.git'
license=(Apache-2.0)
makedepends=(cmake make python python-lxml libx11 libxrandr wayland git ninja vulkan-headers vulkan-icd-loader gcc gcc-libs)
depends=(gcc-libs llvm-libs libx11 vulkan-headers vulkan-icd-loader)
provides=(vulkan-extensionlayers vulkan-extensionlayers-git)
source=(git+https://github.com/KhronosGroup/Vulkan-ExtensionLayer.git)
md5sums=(SKIP)

pkgver(){
  cd ${srcdir}/Vulkan-ExtensionLayer
  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 --exclude sdk-* | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd ${srcdir}/Vulkan-ExtensionLayer
  scripts/update_deps.py
}

build(){
  cd ${srcdir}/Vulkan-ExtensionLayer

  rm -rf build_64

  cmake -C helper.cmake -H. -G Ninja -Bbuild_64 \
  -DCMAKE_C_FLAGS=-m64 \
  -DCMAKE_CXX_FLAGS=-m64 \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DVULKAN_HEADERS_INSTALL_DIR=/usr \
  -DCMAKE_INSTALL_LIBDIR=lib \
  -DCMAKE_INSTALL_SYSCONFDIR=/etc \
  -DCMAKE_INSTALL_DATADIR=/share \
  -DCMAKE_INSTALL_INCLUDEDIR="/usr/include/" \
  -DBUILD_LAYERS=ON \
  -DBUILD_LAYER_SUPPORT_FILES=ON \
  -DCMAKE_SKIP_RPATH=True \
  -DCMAKE_BUILD_TYPE=Debug \
  -DUSE_CCACHE=OFF \
  -DBUILD_TESTS=OFF \
  -DINSTALL_TESTS=OFF

  ninja -C build_64
}

package_vulkan-extensionlayers-git(){
  DESTDIR="$pkgdir" ninja -C ${srcdir}/Vulkan-ExtensionLayer/build_64/ install

  # install licence
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/Vulkan-ExtensionLayer/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/
}
