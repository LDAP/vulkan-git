# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-headers-git
pkgdesc='Vulkan header files (git version)'
pkgver=1.3.262
pkgrel=1
arch=(any)
url='https://github.com/KhronosGroup/Vulkan-Headers'
license=(Apache-2.0)
makedepends=(cmake git ninja make)
conflicts=(vulkan-headers)
provides=(vulkan-hpp vulkan-headers vulkan-headers-git)
source=(git+https://github.com/KhronosGroup/Vulkan-Headers.git)
md5sums=(SKIP)

pkgver(){
  cd "${srcdir}"/Vulkan-Headers

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 --exclude sdk-* | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/Vulkan-Headers \
  -G "Unix Makefiles" \
  -D CMAKE_BUILD_TYPE=Release \
  -D CMAKE_INSTALL_PREFIX=/usr \
  -D CMAKE_INSTALL_LIBDIR=lib

  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  # install licence
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/Vulkan-Headers/LICENSES/* "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

# vim:set ts=8 sts=2 sw=2 et:
