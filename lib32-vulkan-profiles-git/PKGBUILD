#Maintainer: archdevlab <https://github.com/archdevlab>

pkgname=lib32-vulkan-profiles-git
pkgdesc='Vulkan Profiles Toolset (32-bit) (git version)'
pkgver=1.3.268.0.r94.g0255c1a
pkgrel=1
arch=(i686 x86_64)
url='https://github.com/KhronosGroup/Vulkan-Profiles'
license=(Apache-2.0 BSD-2 BSL-1.0)
makedepends=(cmake git ninja make valijson jsoncpp python-jsonschema vulkan-headers lib32-vulkan-icd-loader lib32-vulkan-utility-libraries)
depends=(lib32-gcc-libs lib32-vulkan-icd-loader vulkan-headers lib32-vulkan-utility-libraries lib32-libx11)
conflicts=(lib32-vulkan-profiles)
provides=(lib32-vulkan-profiles)
source=(git+https://github.com/KhronosGroup/Vulkan-Profiles.git)

pkgver(){
  cd "${srcdir}"/Vulkan-Profiles
  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^vulkan-sdk-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
export CFLAGS+=" -m32"
export CXXFLAGS+=" -m32"
export LDFLAGS+=" -m32"
export ASFLAGS+=" --32"
export CFLAGS+=" -fPIC"
export CXXFLAGS+=" -fPIC"
export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/Vulkan-Profiles \
  -D UPDATE_DEPS=OFF \
  -G "Unix Makefiles" \
  -D CMAKE_C_FLAGS=-m32 \
  -D CMAKE_CXX_FLAGS=-m32 \
  -D CMAKE_BUILD_TYPE=Release \
  -D CMAKE_INSTALL_PREFIX=/usr \
  -D CMAKE_INSTALL_LIBDIR=lib32 \
  -D CMAKE_INSTALL_SYSCONFDIR=/etc \
  -D CMAKE_INSTALL_DATADIR=/share \
  -D CMAKE_INSTALL_INCLUDEDIR="/usr/include/" \
  -D VULKAN_HEADERS_INSTALL_DIR=/usr \
  -D VULKAN_UTILITY_LIBRARIES_INSTALL_DIR=/usr \
  -D VULKAN_LOADER_INSTALL_DIR=/usr \
  -D JSONCPP_INSTALL_DIR=/usr \
  -D VALIJSON_INSTALL_DIR=/usr \
  -D BUILD_TESTS=OFF \
  -D ANDROID=OFF \
  -D BUILD_TESTS_EXTRA=OFF \
  -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/Vulkan-Profiles/LICENSES/* "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
